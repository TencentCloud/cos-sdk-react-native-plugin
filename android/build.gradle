buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["QCloudCosReactNative_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["QCloudCosReactNative_" + name]).toInteger()
}

android {
  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

}

// 定义一个方法, 用于获取当前moudle的dir
def getCurrentProjectDir() {
  String result = ""
  rootProject.allprojects { project ->
    if (project.properties.get("identityPath").toString() == ":react-native-cos-sdk") { // 这里是react-native的约定, 插件的module名是插件名, :是gradle的约定. project前加:
      result = project.properties.get("projectDir").toString()
    }
  }
  return result
}

rootProject.allprojects {
  // 这个闭包是循环所有project, 我们让这个仓库可以被所有module找到
  def dir = getCurrentProjectDir()
  repositories {
    mavenCentral()
    google()
    maven { // 添加这个指向本地的仓库目录
      url "$dir/repo"
    }
  }
}


dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native"

  implementation 'com.qcloud.cos:qcloud-foundation:1.5.55'
  implementation 'com.qcloud.cos:cos-android-nobeacon:5.9.13'
  implementation 'com.google.code.gson:gson:2.10.1'
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "QCloudCosReactNative"
    codegenJavaPackageName = "com.cosreactnative"
  }
}
